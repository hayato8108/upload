Option Explicit

'CreateSQLQuery
'UnionAnalogTable
'UnionFPTable
'
'CSV1    CSV2    CSV3    UNIT    FOLDER  NAME    EU  COMMENT DEC1    DEC2    DEC3    BINALY
'[FUNC]          =IF(A2'="*",D1,fSplit(A2,1,"#"))    =IF(B2'="*",E1,fSplit(B2,1,"#"))    =fSplit(C2,1,"#")   =fSplitBetween($C2,2,"+EU'=","+C'=")    =fSplit($C2,2,"+C'=")   =fSplitBetween($C2,2,"DEC:","|","TRUE") =fSplitBetween($C2,2,"|","|","TRUE")    =IF(fSplit(C2,1,"DEC")'=C2,"",fSplitBetween($C2,3,"|","+EU",TRUE))  =fReplaceRegx(C2,".*(BIN-?\d+).*","$1")

Sub GetQueryResults(SQLQuery As String, WorkBookName As String, WorkSheetName As String)

    Dim DataFilePath As String
    Dim cn As ADODB.Connection
    Dim rs As ADODB.Recordset
    Dim ws As Worksheet
    Dim i As Integer
    Dim RowCount As Long, ColCount As Long
    
    'Exit the procedure if no query was passed in
    If SQLQuery = "" Then
            DebugOut _
                inptCategory:="SQL_ERR", _
                inptMsg:=">>> Query string missing, You didn't enter a query"
        Exit Sub
    End If
    
    'Check that the Movies workbook exists in the same folder as this workbook
    DataFilePath = WorkBookName
    
    If Dir(DataFilePath) = "" Then
        DebugOut _
            inptCategory:="SQL_ERR", _
            inptMsg:=">>> File not found, & Dir(DataFilePath)"
        Exit Sub
    End If
    
    'Create and open a connection to the Movies workbook
    Set cn = New ADODB.Connection
    cn.ConnectionString = _
        "Provider=Microsoft.ACE.OLEDB.12.0;" & _
        "Data Source=" & DataFilePath & ";" & _
        "Extended Properties='Excel 12.0 Xml;HDR=YES';"
    
    'Try to open the connection, exit the subroutine if this fails
    On Error GoTo EndPoint
    cn.Open
    
    'If anything fails after this point, close the connection before exiting
    On Error GoTo CloseConnection
    
    'Create and populate the recordset using the SQLQuery
    Set rs = New ADODB.Recordset
    rs.ActiveConnection = cn
    rs.CursorType = adOpenStatic
    
    rs.Source = SQLQuery    'Use the query string that we passed into the procedure
    
    'Try to open the recordset to return the results of the query
    rs.Open
    
    'If anything fails after this point, close the recordset and connection before exiting
    On Error GoTo CloseRecordset
    
    'Get count of rows returned by the query
    RowCount = rs.RecordCount
    
    'Debug.Print RowCount & " row(s)", SQLQuery
    
    'Exit the procedure if no rows returned
    If RowCount = 0 Then
        DebugOut _
            inptCategory:="SQL_ERR", _
            inptMsg:=">>> The query returned no results"
        Exit Sub
    End If
    
    'Get the count of columns returned by the query
    ColCount = rs.Fields.Count
    
    'Create a new worksheet
    'Set ws = ThisWorkbook.Worksheets.Add
    Set ws = ThisWorkbook.Worksheets(WorkSheetName)
    
    'Select the worksheet to avoid the formatting bug with CopyFromRecordset
    Workbooks(Dir(DataFilePath)).Activate
    ws.Select
    ws.Range("A1:XFD" & Rows.Count).Clear
    
    'Format the header row of the worksheet
    With ws.Range("A1").Resize(1, ColCount)
        .Interior.Color = rgbCornflowerBlue
        .Font.Color = rgbWhite
        .Font.Bold = True
    End With
    
    'Copy values from the recordset into the worksheet
    ws.Range("A2").CopyFromRecordset rs
    
    'Write column names into row 1 of the worksheet
    For i = 0 To ColCount - 1
        With rs.Fields(i)
            ws.Range("A1").Offset(0, i).Value = .Name
            
            'Apply a custom date format to date columns
            If .Type = adDate Then
                ws.Range("A1").Offset(1, i).Resize(RowCount, 1).NumberFormat = "dd mmm yyyy"
            End If
        End With
    Next i
    
    'Change the column widths on the worksheet
    ws.Range("A1").CurrentRegion.EntireColumn.AutoFit
    
    'Close the recordset and connection
    'This will happen anyway when the local variables go out of scope at the end of the subroutine
    rs.Close
    cn.Close
    
    'Free resources used by the recordset and connection
    'This will happen anyway when the local variables go out of scope at the end of the subroutine
    Set rs = Nothing
    Set cn = Nothing
    
    'Exit here to make sure that the error handling code does not run
    Exit Sub
    
'========================================================================
'ERROR HANDLERS
'========================================================================
CloseRecordset:
'If the recordset is opened successfully but a runtime error occurs later we end up here
    rs.Close
    cn.Close
    
    Set rs = Nothing
    Set cn = Nothing
    
'    Debug.Print SQLQuery
    DebugOut _
        inptCategory:="SQL_ERR", _
        inptMsg:=">>> An error occurred after the recordset was opened." _
            & vbNewLine & "             Error number: " & Err.Number _
            & vbNewLine & "             Error description: " & Err.Description
    Exit Sub

CloseConnection:
'If the connection is opened successfully but a runtime error occurs later we end up here
    cn.Close
    
    Set cn = Nothing
    
   ' Debug.Print SQLQuery
    DebugOut _
        inptCategory:="SQL_ERR", _
        inptMsg:=">>> An error occurred after the connection was established." _
            & vbNewLine & "             Error number: " & Err.Number _
            & vbNewLine & "             Error description: " & Err.Description
    Exit Sub
    
'If the connection failed to open we end up here
EndPoint:
    DebugOut _
        inptCategory:="SQL_ERR", _
        inptMsg:=">>> The connection failed to open." _
            & vbNewLine & "             Error number: " & Err.Number _
            & vbNewLine & "            Error description: " & Err.Description
            
End Sub


